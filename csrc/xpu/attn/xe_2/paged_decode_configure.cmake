function(paged_decode_configure FILENAME_SUFFIX)
set(GEN_KERNEL_SRCS)  # output
set(L_BOOLS "false" "true")
set(BOOL_FLAG_false "f")
set(BOOL_FLAG_true "t")

set(qgroup_list "_8" "_16")
set(headsize_list "_64" "_96" "_128" "_192" "_256")

foreach(IMPL_QGROUP ${qgroup_list})
foreach(IMPL_HEADSIZE ${headsize_list})
foreach(IMPL_KISCAUSAL ${L_BOOLS})
foreach(IMPL_KISLOCAL ${L_BOOLS})
foreach(IMPL_KISSINK ${L_BOOLS})
    set(FILE_SUFFIX "_qgroup${IMPL_QGROUP}_head${IMPL_HEADSIZE}_")
    set(FILE_SUFFIX "${FILE_SUFFIX}${BOOL_FLAG_${IMPL_KISCAUSAL}}")
    set(FILE_SUFFIX "${FILE_SUFFIX}${BOOL_FLAG_${IMPL_KISLOCAL}}")
    set(FILE_SUFFIX "${FILE_SUFFIX}${BOOL_FLAG_${IMPL_KISSINK}}")
    configure_file(${FILENAME_SUFFIX}.cpp.in "${FILENAME_SUFFIX}${FILE_SUFFIX}.cpp")
    list(APPEND GEN_KERNEL_SRCS "${CMAKE_CURRENT_BINARY_DIR}/${FILENAME_SUFFIX}${FILE_SUFFIX}.cpp")
endforeach()
endforeach()
endforeach()
endforeach()
endforeach()

list(REMOVE_DUPLICATES GEN_KERNEL_SRCS)
list(LENGTH GEN_KERNEL_SRCS GEN_KERNEL_SRCS_LENGTH)
message(STATUS "Generated ${FILENAME_SUFFIX} kernel sources: ${GEN_KERNEL_SRCS_LENGTH}")
set(GEN_KERNEL_SRCS ${GEN_KERNEL_SRCS} PARENT_SCOPE)
set(GEN_KERNEL_SRCS_LENGTH ${GEN_KERNEL_SRCS_LENGTH} PARENT_SCOPE)

list(APPEND ATTN_KERNEL_SRCS_GEN ${GEN_KERNEL_SRCS})
set(ATTN_KERNEL_SRCS_GEN ${ATTN_KERNEL_SRCS_GEN} PARENT_SCOPE)
message(STATUS "Total ATTN kernel sources after ${FILENAME_SUFFIX}: ${ATTN_KERNEL_SRCS_GEN}")
endfunction()
